apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def config = rootProject.ext

android {
    compileSdkVersion config.appCompileSdkVersion
    buildToolsVersion config.appBuildToolsVersion

    defaultConfig {
        applicationId config.appApplicationId
        minSdkVersion config.appMinSdkVersion
        targetSdkVersion config.appTargetSdkVersion
        versionCode config.appVersionCode
        versionName config.appVersionName
        // All flavors must now belong to a named flavor dimension.
        flavorDimensions "versionCode"
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res']
        }
        apple.res.srcDirs = ['src/main/res-apple']
        orange.res.srcDirs = ['src/main/res-orange']
    }

//signingConfigs的前后顺序不能乱
    signingConfigs {
        staging.initWith(signingConfigs.debug)
        release {
            storeFile file("keystore/release.keystore")
            storePassword "123456"
            keyAlias "releasepwd"
            keyPassword "123456"
        }
        debug {
            storeFile file("keystore/debug.keystore")
            storePassword "123456"
            keyAlias "debugpwd"
            keyPassword "123456"
        }
    }
    buildTypes {
        release {
            zipAlignEnabled true
            // 移除无用的resource文件 如果要使用shrinkResources 必须使minifyEnabled为true
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            buildConfigField "boolean", "DEBUG_ENABLE", 'false'
            buildConfigField("String", "BASE_URL", '"http://one.netpulse.com"')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "DEBUG_ENABLE", 'true'
            buildConfigField("String", "BASE_URL", '"http://one.planetfitness.com"')
        }
    }

//根据不同的Flavor和不同的module对App的资源进行替换和逻辑变换
    productFlavors {
        offical{

        }
        apple {

        }
        orange {

        }
    }

// 根据Flavor设置在清单文件中进行设置
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name, APP_NAME: name]
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "aragoncs-${variant.buildType.name}-${releaseTime()}-${defaultConfig.versionName}-${variant.productFlavors[0].name}.apk"
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation project(':repository:viewpagerindicator')
}
